plugins {
	id 'checkstyle'
	id 'pmd'
	id 'jacoco'
	id 'org.springframework.boot' version '2.7.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'java-library'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.8.5'
	id 'org.owasp.dependencycheck' version '7.3.2'
	id 'com.github.ben-manes.versions' version '0.44.0'
	id 'org.sonarqube' version '3.5.0.2730'
	id 'info.solidsoft.pitest' version '1.9.11'
}

group = 'uk.gov.hmcts.reform.mi'
version = '3.6.2'
sourceCompatibility = '11'

bootJar {
	enabled = false
}

jar {
	enabled = true
	archiveClassifier = ''
	archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

checkstyle {
	maxWarnings = 0
	toolVersion = '8.24'
	// need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
	configDirectory = new File(rootDir, 'config/checkstyle')
}

pmd {
	toolVersion = "6.41.0"
	sourceSets = [sourceSets.main, sourceSets.test]
	reportsDir = file("$project.buildDir/reports/pmd")
	// https://github.com/pmd/pmd/issues/876
	ruleSets = []
	ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
	executionData(test)
	reports {
		xml.enabled = true
		csv.enabled = false
		xml.destination file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
		html.destination file("${buildDir}/reports/jacoco/jacocoTestReport.html")
	}
}

project.tasks['sonarqube'].dependsOn test

def sonarExclusions = [
	'**uk/gov/hmcts/reform/mi/micore/MiCoreLibConfigurationContext.java',
	'**uk/gov.hmcts/reform/mi/micore/utils/AzureWrapper.java'
]

sonarqube {
	properties {
		property "sonar.projectName", "Reform :: MI Core Library"
		property "sonar.projectKey", "uk.gov.hmcts.reform:mi-core-lib"
		property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/jacocoTestReport.xml"
		property "sonar.exclusions", sonarExclusions.join(", ")
	}
}

dependencyUpdates.resolutionStrategy {
	componentSelection { rules ->
		rules.all {
			def isNonStable = { String version ->
				['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
					version ==~ /(?i).*[.-]\$qualifier[.\\d-+]*/
				}
			}
			if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
				selection.reject('Release candidate')
			}
		}
	}
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
	// Specifies if the build should be failed if a CVSS score above a specified level is identified.
	// range of 0-10 fails the build, anything greater and it doesn't fail the build
	failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
	suppressionFile = 'config/owasp/suppressions.xml'
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

def gitRepo = "https://github.com/hmcts/mi-core-lib"

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourcesJar
			artifact javadocJar

			pom {
				name = 'mi-core-lib'
				description = 'HMCTS Management Information Core Libraries and Utilities'
				url = gitRepo
				licenses {
					license {
						name = 'MIT License, Copyright (c) 2019 HM Courts & Tribunals Service'
						url = "http://www.opensource.org/licenses/mit-license.php"
					}
				}
				scm {
					url = gitRepo
				}
			}
		}
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['mavenJava']
	publish = true
	pkg {
		repo = 'hmcts-maven'
		name = 'mi-core-lib'
		userOrg = 'hmcts'
		licenses = ['MIT']
		vcsUrl = gitRepo
		version {
			name = project.version
		}
	}
}

repositories {
	mavenLocal()
	jcenter()
	mavenCentral()
	maven {
		url 'https://jitpack.io'
	}
}

ext {
	set('azureVersion', "4.0.0")
}

dependencyManagement {
	dependencies {
		dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.70'
		dependency group: 'commons-io', name: 'commons-io', version: '2.11.0'
		// CVE-2018-10237 - Unbounded memory allocation
		dependencySet(group: 'com.google.guava', version: '31.0.1-jre') {
			entry 'guava'
		}
		dependencySet(group: 'org.apache.logging.log4j', version: '2.17.1') {
			entry 'log4j-to-slf4j'
			entry 'log4j-api'
		}
		dependencySet(group: 'io.netty', version: '4.1.72.Final') {
			entry 'netty-buffer'
			entry 'netty-codec'
			entry 'netty-codec-dns'
			entry 'netty-codec-http'
			entry 'netty-codec-http2'
			entry 'netty-codec-socks'
			entry 'netty-common'
			entry 'netty-handler'
			entry 'netty-handler-proxy'
			entry 'netty-resolver'
			entry 'netty-resolver-dns'
			entry 'netty-resolver-dns-classes-macos'
			entry 'netty-resolver-dns-native-macos'
			entry 'netty-transport'
			entry 'netty-transport-classes-epoll'
			entry 'netty-transport-native-epoll'
			entry 'netty-transport-classes-kqueue'
			entry 'netty-transport-native-kqueue'
			entry 'netty-transport-native-unix-common'
		}
	}
	imports {
		mavenBom "com.azure.spring:azure-spring-boot-bom:${azureVersion}"
	}
}

def versions = [
	azureCore     	 : '1.34.0',
	azureIdentity 	 : '1.4.2',
	azureSpringBoot	 : '3.14.0',
	azureStorageBlob : '12.14.2',
	azureStorageCom  : '12.14.1',
	commonsIo        : '2.11.0',
	commonsLang      : '3.12.0',
	hmctsLogging     : '5.1.9',
	junit         	 : '5.8.2',
	lombok        	 : '1.18.24',
	mockito       	 : '4.1.0',
	openCsv       	 : '5.5.2',
	piTest        	 : '1.7.3',
	piTestGradle  	 : '1.7.0',
	piTestSonar   	 : '0.5'
]

dependencies {
	// Spring Boot Initial Dependencies
	implementation group: 'com.azure.spring', name: 'azure-spring-boot-starter-storage', version: versions.azureSpringBoot

	// Azure Dependencies
	implementation group: 'com.azure', name: 'azure-core', version: versions.azureCore
	implementation group: 'com.azure', name: 'azure-storage-blob', version: versions.azureStorageBlob
	implementation group: 'com.azure', name: 'azure-storage-common', version: versions.azureStorageCom
	implementation group: 'com.azure', name: 'azure-identity', version: versions.azureIdentity

	// Apache Commons
	implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang

	// CSV Dependencies
	implementation group: 'com.opencsv', name: 'opencsv', version: versions.openCsv

	// Lombok
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
	implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

	// HMCTS Logging
	implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.hmctsLogging
	implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.hmctsLogging

	// Test Dependencies
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
	testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

	testImplementation group: 'org.mockito', name: 'mockito-core', version: versions.mockito
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: versions.mockito

	testImplementation group: 'org.pitest', name: 'pitest', version: versions.piTest
	testImplementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.piTestGradle
	testImplementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: versions.piTestSonar
}

test {
	useJUnitPlatform()
}

pitest {
	targetClasses = ['uk.gov.hmcts.reform.mi.micore.component.*',
					 'uk.gov.hmcts.reform.mi.micore.exception.*',
					 'uk.gov.hmcts.reform.mi.micore.factory.*',
					 'uk.gov.hmcts.reform.mi.micore.utils.AuthTokenUtils*']
	testSourceSets = [sourceSets.test]
	threads = 10
	outputFormats = ['XML', 'HTML']
	timestampedReports = false
	mutationThreshold = 80
	verbose = true
	junit5PluginVersion = '0.12'
}
